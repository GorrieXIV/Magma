/* All reduced weylwords of a given element 
  * 
  * Equivalent to section 5.3.2 of the LiE manual.
  */
 D4 := RootDatum("D4" : Isogeny := "SC");
WD4 := CoxeterGroup(D4);
D4fp, hm := CoxeterGroup(GrpFPCox, WD4);
ToWeylWord := function(W, w) return &*[W.i : i in w]; end function;
reduce_function := function(W) fp, hm := CoxeterGroup(GrpFPCox, W); return func< w | Eltseq(hm(ToWeylWord(W, w))) >; end function;
reduce_function(WD4)([1,2,2,3]);


/* All reduced weyl words of a given element */
next_rewrite := function(W, reduce, w, k)
    if k+1 gt #w then return w; end if;
    m := CoxeterMatrix(W); m := m[w[k],w[k+1]];
    if k+m-1 gt #w then return w; end if;
    for i in [k+2..k+m-1] do
        if w[i] ne w[i-2] then return w; end if;
    end for;
    t := w[k+m-2];
    for i in [k..k+m-2] do w[i] := w[i+1]; end for;
    w[k+m-1] := t;
    return w;
end function;
all_rewrites := function(W, w)
    reduce := reduce_function(W);
    w := reduce(w);
    l := #w; m := [w]; i := 0;
    while i lt #m do
        i +:= 1;
        for j in [1..l-1] do
            next := next_rewrite(W, reduce, m[i], j);
            p := Position(m, next);
            if (p eq 0) then Append(~m, next); end if;
            if (#m mod 1000 eq 0) then print #m; end if;
        end for;
    end while;
    return m;
end function;
 
 all_rewrites(WD4, [1,2,3,4,2]);

#all_rewrites(CoxeterGroup(RootDatum("A3")), [1,2,1,3,2,1]);

 num := function(lietype, lierank)
    R := RootDatum(lietype cat IntegerToString(lierank) : Isogeny := "SC");
    W := CoxeterGroup(R);
    return #all_rewrites(W, LongestWeylWord(R));
end function;
[ num("A", i) : i in [1..4] ];

/* Calculating this result will take several hours 
    > [ num("A", i) : i in [1..5] ];
    [ 1, 2, 16, 768, 292864 ]

    This result corresponds to sequence A005118 of "The On-Line Encyclopedia 
    of Integer Sequences": Number of simple allowable sequences on 1..n
    containing the permutation 12...n.
    
    See http://www.research.att.com/~njas/sequences/A005118
    
    > f := func<n | Factorial(Binomial(n, 2)) / ( &*[ (2*i + 1)^(n-i-1) : i in [0..(n-2)] ]) >;
    > [ f(i) : i in [2..6] ];
    [ 1, 2, 16, 768, 292864 ]
 */
